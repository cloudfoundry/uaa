description = 'CloudFoundry Identity Client Library Jar'

dependencies {
  compile project(':cloudfoundry-identity-model')
  compile group: 'org.apache.httpcomponents', name: 'httpclient', version: parent.commonsHttpClientVersion
  testCompile group: 'junit', name: 'junit', version: parent.junitVersion
  testCompile project(':cloudfoundry-identity-model').configurations.testCompile.dependencies
  testCompile project(':cloudfoundry-identity-model').sourceSets.test.output
}

processResources {
  //maven replaces project.artifactId in the log4j.properties file
  //https://www.pivotaltracker.com/story/show/74344574
  filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}', 'cloudfoundry-identity-clientlib') : line }
}

test {
  exclude 'org/cloudfoundry/identity/client/integration/*.class'
}

integrationTest {
  filter {
    includeTestsMatching "org.cloudfoundry.identity.client.integration.*"
  }
}

publishing {
  publications {
    lib(MavenPublication) {
      groupId 'org.cloudfoundry.identity'
      artifactId 'cloudfoundry-identity-client-lib'

      from components.java
      artifact sourceJar
    }
  }
}