import org.apache.tools.ant.filters.ReplaceTokens

import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    apply(from: "dependencies.gradle")

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url("https://plugins.gradle.org/m2/")
        }
        maven {
            url("https://repo.spring.io/plugins-release")
        }
        maven {
            url("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath(libraries.cargoGradlePlugin)
        classpath(libraries.prodepsGradlePlugin)
        classpath(libraries.asciidoctorGradlePlugin)
        classpath(libraries.springDependencyMangementGradlePlugin)
        classpath(libraries.springBootGradlePlugin)
        classpath(libraries.testRetryPlugin)
        classpath(libraries.coverallsGradlePlugin)
    }
}

apply from: file('docker.gradle')
apply(from: "dependencies.gradle")

def applicationPort = project.hasProperty('port') ? project.getProperty('port').toInteger() : 8080

apply(plugin: "com.bmuschko.cargo")

def geArtifactoryReader = System.getenv('ARTIFACTORY_READER')
def geArtifactoryReaderPassword  = System.getenv('ARTIFACTORY_READER_PW')
def proxyHost  = System.getenv('PROXY_HOST')
def proxyPort  = System.getenv('PROXY_PORT')
def nonProxyHosts = 'localhost|*.localhost'

allprojects {
    apply(plugin: "propdeps")
    apply(plugin: "propdeps-maven")
    apply(plugin: "io.spring.dependency-management")
    apply(plugin: "org.gradle.test-retry")
    apply(plugin: "jacoco")

    dependencyManagement {
        imports {
            mavenBom(libraries.springBom)
            mavenBom(libraries.springBootBom)
        }
    }

    configurations.provided.transitive = false

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            credentials {
                username geArtifactoryReader
                password geArtifactoryReaderPassword
            }
            url 'https://artifactory.build.ge.com/predix-virtual'
            content {
                // There is a discrepancy in javax/validation/validation-api/maven-metadata.xml
                // for latest version of validation-api
                // between maven repo (2.0.1.Final) and predix-virtual (2.0.2-SNAPSHOT).
                // Prefer version from maven repo.
                excludeGroup "javax.validation"
            }
        }
        // shibboleth.net is not reachable from the Jenkins nodes due to GE selling "3.0.0.0/8 address space" to Amazon.
        // Now, within GE, when Propel sends the wget command to the 3.213.250.186 address that shibboleth has,
        // it never makes it out of the GE network because it thinks 3.x.x.x is within GE network.
        // See https://docs.gecloudpod.com/misc/3pocalypse
        // As of August 2020, relevant dependencies were uploaded to GE artifactory and should be available in predix-virtual
        // maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        maven { url 'https://artifacts.alfresco.com/nexus/content/repositories/public/' }
        maven { url 'https://repository.mulesoft.org/releases/' }
    }
}

apply(plugin: "jacoco")
apply(plugin: "com.github.kt3k.coveralls")

subprojects {
    apply(plugin: "java")

    configurations.all {
        exclude(group: "org.hamcrest", module: "hamcrest-all")
        exclude(group: "org.hamcrest", module: "hamcrest-core")
        exclude(group: "org.hamcrest", module: "hamcrest-library")
        exclude(group: "org.springframework.boot", module: "spring-boot-starter-logging")
        exclude(group: "org.apache.directory.server", module: "apacheds-core")
        exclude(group: "org.apache.directory.server", module: "apacheds-protocol-ldap")
        exclude(group: "org.skyscreamer", module: "jsonassert")
        exclude(group: "com.vaadin.external.google", module: "android-json")
    }

    dependencies {
        testImplementation(libraries.springBootStarterTest)
        testImplementation(libraries.hamcrest)
        testImplementation(libraries.junit5JupiterApi)
        testImplementation(libraries.junit5JupiterParams)
        testRuntimeOnly(libraries.junit5JupiterEngine)
        testRuntimeOnly(libraries.junitVintageEngine)
        testImplementation(libraries.unboundIdLdapSdk)

        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:none", "-nowarn"]

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    test {
        maxParallelForks = 6
        useJUnitPlatform()
        jvmArgs += ["-Xmx1024m", "-XX:+StartAttachListener", "-XX:+HeapDumpOnOutOfMemoryError", "-XX:HeapDumpPath=/var/log/uaa-tests.hprof"]

        retry {
            failOnPassedAfterRetry = Boolean.parseBoolean(System.getProperty("failOnPassedAfterRetry", "true"));
            maxFailures = Integer.parseInt(System.getProperty("maxFailures", "3"))
            maxRetries = Integer.parseInt(System.getProperty("maxRetries", "1"))
        }

        testLogging {
            events("skipped", "failed", "passed")
            exceptionFormat("full")

            // Uncomment the following line to see all standard output from tests (there's a ton of it!)
            //showStandardStreams = true
        }
    }

    task integrationTest(type: Test) {
        dependsOn rootProject.cargoStartLocal
        useJUnitPlatform()

        testLogging {
            events("skipped", "failed", "passed")
            exceptionFormat("full")

            // Uncomment the following line to see all standard output from tests (there's a ton of it!)
            //showStandardStreams = true
        }
    }

    task generateDocs() {}

    task allDeps(type: DependencyReportTask) {}

    task writeNewPom() {
        doLast {
            pom {
                project {
                    licenses {
                        license {
                            name("The Apache Software License, Version 2.0")
                            url("http://www.apache.org/licenses/LICENSE-2.0.txt")
                            distribution("repo")
                        }
                    }
                }
            }.writeTo("./pom.xml")
        }
    }

    repositories {
        mavenCentral()
        maven {
            url("https://jitpack.io")
        }
        maven {
            url("https://repo.spring.io/release")
        }
        maven {
            url("https://repo.maven.apache.org/maven2")
        }
        maven {
            credentials {
                username geArtifactoryReader
                password geArtifactoryReaderPassword
            }
            url 'https://artifactory.build.ge.com/predix-virtual'
        }
    }

    tasks.withType(Test) {
        systemProperties = System.properties
        systemProperties['user.dir'] = workingDir
    }

    jacocoTestReport {
        additionalSourceDirs(files(sourceSets.main.allSource.srcDirs))
        sourceDirectories.setFrom(files(sourceSets.main.allSource.srcDirs))
        classDirectories.setFrom(files(sourceSets.main.output))
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
        getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
    }

    if (!path.startsWith(':cloudfoundry-identity-samples')) {
        task collectDependencies(type: Copy) {
            from configurations.runtime
            into "${rootProject.buildDir}/dependencies"
            exclude 'cloudfoundry-identity-*.jar'
        }
    }
}

def jacocoJarPath = project.zipTree(configurations.jacocoAgent.singleFile).filter({
    it.name == 'jacocoagent.jar'
}).asPath
def jacocoLocation = System.getProperty('java.io.tmpdir') + "/jacocoagent.jar"

def integrationTestCoverageExecutionData = "${buildDir}/integrationTestCoverageReport.exec"

cargo {
    containerId = "tomcat9x"
    port = applicationPort

    if (JavaVersion.current() < JavaVersion.VERSION_11) {
        throw new GradleException("This build must be run with Java version [ " + JavaVersion.VERSION_11 + " ] or greater. Your Java version is [ " + JavaVersion.current() + " ]")
    }

    deployable {
        file = file("samples/api/build/libs/cloudfoundry-identity-api-" + version + ".war")
        context = "api"
    }

    deployable {
        file = file("samples/app/build/libs/cloudfoundry-identity-app-" + version + ".war")
        context = "app"
    }

    deployable {
        file = file("uaa/build/libs/cloudfoundry-identity-uaa-" + version + ".war")
        context = "uaa"
    }

    local {
        configHomeDir = file(Paths.get(System.getProperty("java.io.tmpdir") + "/uaa-${applicationPort}"))
        startStopTimeout = Integer.parseInt(System.getProperty("startStopTimeout", "540000"))
        rmiPort = applicationPort + 10

        jvmArgs = ""
        jvmArgs = String.format("%s -javaagent:%s=output=file,dumponexit=true,append=false,destfile=%s", jvmArgs,
            file(jacocoLocation).getAbsolutePath(), integrationTestCoverageExecutionData)
        jvmArgs = String.format("%s -DCLOUDFOUNDRY_CONFIG_PATH=%s", jvmArgs, file("scripts/cargo").getAbsolutePath())
        jvmArgs = String.format("%s -Dlogging.config=%s", jvmArgs, file("scripts/cargo/log4j2.properties").getAbsolutePath())
        jvmArgs = String.format("%s -Dstatsd.enabled=true", jvmArgs)
        if (System.getProperty("spring.profiles.active", "").split(',').contains("debug")) {
            jvmArgs = String.format("%s -agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005", jvmArgs)
        }
        else if (Boolean.valueOf(System.getProperty("xdebug"))) {
            jvmArgs = String.format("%s -Xdebug " +
                    "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005 " +
                    "-Xnoagent -Djava.compiler=NONE", jvmArgs)
        }

        outputFile = file("uaa/build/reports/tests/uaa-server.log")
        configFile {
            files = files("scripts/cargo/tomcat-conf/context.xml")
            toDir = "conf"
        }

        systemProperties {
            property("SECRETS_DIR", System.getProperty("SECRETS_DIR", file("scripts/cargo").getAbsolutePath()))
            property("spring.profiles.active", System.getProperty("spring.profiles.active", "default,nurego"))
            property("metrics.perRequestMetrics", System.getProperty("metrics.perRequestMetrics", "true"))
            property("smtp.host", "localhost")
            property("smtp.port", 2525)
            property("java.security.egd", "file:/dev/./urandom")

            if (proxyHost != null && proxyHost.trim() != '') {
                property 'http.proxyHost', proxyHost
                property 'http.proxyPort', proxyPort
                property 'https.proxyHost', proxyHost
                property 'https.proxyPort', proxyPort
                property 'http.nonProxyHosts', nonProxyHosts
            }
            property 'NUREGO_BATCH_INTERVAL_SECONDS', '1'
            property 'NUREGO_BATCH_MAX_MAP_SIZE', '1'
            property 'METER_BASE_DOMAIN', 'localhost'
        }

        containerProperties {
            property('cargo.tomcat.ajp.port', applicationPort + 20)
        }

        installer {
            installUrl = "https://repo1.maven.org/maven2/org/apache/tomcat/tomcat/" + versions.tomcatCargoVersion + "/tomcat-" + versions.tomcatCargoVersion + ".tar.gz"
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
    }
}

project.gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
    project.allprojects.collect({ it.tasks.withType(Test) }).flatten().each {
        it.systemProperty("spring.profiles.active", System.getProperty("spring.profiles.active", "default,nurego"))
        it.systemProperty("testId", System.getProperty("testId", ""))
    }
}

//task declarations
task run(dependsOn: cargoRunLocal)

task manifests(dependsOn: assemble, type: Copy) {
    from("uaa/src/test/resources/sample-manifests") {
        include("**/*.yml")
        filter(ReplaceTokens,
                tokens: [
                        version  : version,
                        app      : System.getProperty("app", "myuaa"),
                        appdomain: System.getProperty("app-domain", "bosh-lite.com"),
                ]
        )
    }
    into("build/sample-manifests")
}

task cleanCargoConfDir {
    delete(file(System.getProperty("java.io.tmpdir") + "/uaa-${applicationPort}"))
    delete file(jacocoLocation)
    try {
        Files.createDirectory(Paths.get(System.getProperty("java.io.tmpdir") + "/uaa-${applicationPort}"))
    } catch (ignored) {}
    delete file(jacocoLocation)
    Files.copy(Paths.get(jacocoJarPath), Paths.get(jacocoLocation))
}

def publishedProjects = subprojects

task integrationTest(type: Test, dependsOn: subprojects.integrationTest) {
    finalizedBy cargoStopLocal
}

def jacocoRootReportConfiguration = {
    additionalSourceDirs(files(subprojects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(subprojects.sourceSets.main.output))
    executionData(files(subprojects.jacocoTestReport.executionData))
    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml")
        csv.enabled = false
    }
}

task jacocoRootReportIntegrationTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn = [integrationTest]
}

task jacocoRootReportTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn = [test]
}

task jacocoRootReportServerTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn(':cloudfoundry-identity-server:test')
}

task jacocoRootReportUaaTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn(':cloudfoundry-identity-uaa:test')
}

task jacocoRootReportDegradedCloudTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn(':cloudfoundry-identity-uaa:degradedTestCloud')
}

task jacocoRootReportDegradedJwtCloudTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn(':cloudfoundry-identity-uaa:degradedJwtTestCloud')
}

task jacocoRootReportAcceptanceTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn(':cloudfoundry-identity-uaa:acceptanceTest')
}

task jacocoRootReportOrchestratorIntegrationTest(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    configure jacocoRootReportConfiguration
    dependsOn(':cloudfoundry-identity-uaa:orchestratorIntegrationTest')
}

coveralls {
    sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'
}

task setupDegradedTestLocal(type: Exec, dependsOn: cargoStartLocal) {
  workingDir 'scripts'
  commandLine './setupDegradedTests.sh'
  finalizedBy 'cargoStopLocal'

  standardOutput = new ByteArrayOutputStream()

  ext.output = {
    return standardOutput.toString()
  }

  doLast {
    println ext.output
  }

}

task setupDegradedTestCloud(type: Exec) {
  workingDir 'scripts'
  commandLine './setupDegradedTests.sh'
}

// task dependencies
assemble.dependsOn(subprojects.assemble)
test.dependsOn(subprojects.test)
test.mustRunAfter(integrationTest)
cargoStartLocal.dependsOn(assemble)
cargoRunLocal.dependsOn(cleanCargoConfDir, assemble)
