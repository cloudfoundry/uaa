apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

description = "CloudFoundry Identity Server JAR"

dependencies {
    compile(project(":cloudfoundry-identity-model"))

    compile(libraries.tomcatJdbc)
    provided(libraries.tomcatEmbed)
    compile(libraries.javaxMail)

    compile(libraries.jsonPath)
    compile(libraries.zxing)
    compile(libraries.springBeans)
    compile(libraries.springContext)
    compile(libraries.springContextSupport)
    compile(libraries.springTx)
    compile(libraries.springJdbc)
    compile(libraries.springWeb)
    compile(libraries.springSecurityCore)
    compile(libraries.springSecurityJwt)
    compile(libraries.springSecurityWeb)
    compile(libraries.springSecuritySaml)
    compile(libraries.springSessionJdbc)

    compile(libraries.springSecurityOauth) {
        exclude(module: "commons-codec")
        exclude(module: "jackson-mapper-asl")
        exclude(module: "spring-security-web")
    }

    compile(libraries.bouncyCastleProv)
    compile(libraries.bouncyCastlePkix)

    compile(libraries.guava)

    compile(libraries.aspectJRt)
    compile(libraries.aspectJWeaver)

    compile(libraries.thymeleafSpring5)
    compile(libraries.thymeleafDialect)
    compile(libraries.thymeleafExtrasSpringSecurity5)

    compile(libraries.unboundIdScimSdk) {
        exclude(module: "servlet-api")
        exclude(module: "commons-logging")
        exclude(module: "httpclient")
        exclude(module: "wink-client-apache-httpclient")
    }

    compile(libraries.hibernateValidator)
    compile(libraries.flywayCore)
    compile(libraries.mariaJdbcDriver)
    compile(libraries.hsqldb)

    compile(libraries.snakeyaml)

    compile(libraries.springSecurityLdap)
    compile(libraries.springLdapCore)
    compile(libraries.springLdapCoreTiger)
    compile(libraries.apacheLdapApi) {
        exclude(module: "slf4j-api")
    }

    compile(libraries.passay)

    compile(libraries.googleAuth)

    compile(libraries.slf4jImpl)
    compile(libraries.log4jCore)

    implementation(libraries.javaxXmlBindApi)
    implementation(libraries.javaxXmlBindCore)
    implementation(libraries.javaxXmlBindImpl)

    testImplementation(project(":cloudfoundry-identity-model").sourceSets.test.output)

    testImplementation(libraries.springTest)

    testImplementation(libraries.junit)
    testImplementation(libraries.mockito)

    testImplementation(libraries.mockitoJunit5)

    testImplementation(libraries.postgresql)

    testImplementation(libraries.tomcatElApi)
    testImplementation(libraries.tomcatJasperEl)
    testImplementation(libraries.tomcatJdbc)

    testImplementation(libraries.jsonPathAssert)
}

configurations.all {
    exclude(group: "org.beanshell", module: "bsh-core")
}

jar {
    exclude("org/cloudfoundry/identity/uaa/web/tomcat/UaaStartupFailureListener.*")
}

processResources {
    //maven replaces project.artifactId in the log4j.properties file
    //https://www.pivotaltracker.com/story/show/74344574
    filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}', 'cloudfoundry-identity-server') : line }
}

integrationTest {}.onlyIf { //disable since we don't have any
    false
}

task tomcatListenerJar(type: Jar) {
    archiveBaseName = "tomcat-listener"
    from(sourceSets.main.output)
    include("org/cloudfoundry/identity/uaa/web/tomcat/UaaStartupFailureListener.*")
}

artifacts {
    archives(tomcatListenerJar)
}

task migrationJar(type: Jar) {
    manifest {
        attributes('Main-Class': "org.cloudfoundry.identity.uaa.db.FlywayMigrationRunner",
                'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' '))
    }
}

shadowJar {
    baseName("UAA-FlywayMigrationRunner")
    manifest {
        inheritFrom(project.tasks.migrationJar.manifest)
    }
    mergeServiceFiles('META-INF/spring.*')
}