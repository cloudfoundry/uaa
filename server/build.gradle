apply plugin: 'java'

description = "CloudFoundry Identity Server JAR"

dependencies {
    implementation(project(":cloudfoundry-identity-metrics-data"))
    implementation(project(":cloudfoundry-identity-model"))

    implementation(libraries.tomcatJdbc)
    provided(libraries.tomcatEmbed)
    implementation(libraries.javaxMail)

    implementation(libraries.jacksonDatabind)
    implementation(libraries.jsonPath)
    implementation(libraries.zxing)
    implementation(libraries.springBeans)
    implementation(libraries.springContext)
    implementation(libraries.springContextSupport)
    implementation(libraries.springTx)
    implementation(libraries.springJdbc)
    implementation(libraries.springWeb)
    implementation(libraries.springSecurityCore)
    implementation(libraries.springSecurityJwt)
    implementation(libraries.springSecurityWeb)
    implementation(libraries.springSecuritySaml) {
        exclude(module: "bcprov-ext-jdk15on")
    }
    implementation(libraries.xmlSecurity)
    implementation(libraries.springSessionJdbc)

    implementation(libraries.springSecurityOauth) {
        exclude(module: "commons-codec")
        exclude(module: "jackson-mapper-asl")
        exclude(module: "spring-security-web")
    }

    implementation(libraries.springCloudServiceConnector)
    implementation(libraries.springCloudCFConnector)

    implementation(libraries.bouncyCastleProv)
    implementation(libraries.bouncyCastlePkix)

    implementation(libraries.guava)

    implementation(libraries.aspectJRt)
    implementation(libraries.aspectJWeaver)

    implementation(libraries.thymeLeaf) {
        exclude(module: "ognl")
    }
    implementation(libraries.thymeleafSpring5) {
        exclude(module: "ognl")
    }
    implementation(libraries.thymeleafDialect) {
        exclude(module: "ognl")
    }
    implementation(libraries.thymeleafExtrasSpringSecurity5) {
        exclude(module: "ognl")
    }

    implementation(libraries.unboundIdScimSdk) {
        exclude(module: "servlet-api")
        exclude(module: "commons-logging")
        exclude(module: "httpclient")
        exclude(module: "wink-client-apache-httpclient")
        exclude(module: "json")
    }

    implementation(libraries.hibernateValidator)
    implementation(libraries.flywayCore)
    implementation(libraries.mariaJdbcDriver)
    implementation(libraries.hsqldb)

    implementation(libraries.snakeyaml)

    implementation(libraries.springSecurityLdap)
    implementation(libraries.springLdapCore)
    implementation(libraries.springLdapCoreTiger)
    implementation(libraries.apacheLdapApi) {
        exclude(module: "slf4j-api")
        exclude(module: "mina-core")
    }

    implementation(libraries.passay)
    implementation(libraries.beanutils)
    implementation(libraries.velocity)
    implementation(libraries.xerces)

    implementation(libraries.googleAuth)

    implementation(libraries.slf4jImpl)
    implementation(libraries.log4jCore)

    implementation(libraries.javaxXmlBindApi)
    implementation(libraries.glassfishJaxb)

    implementation(libraries.nimbusJwt)
    implementation(libraries.orgJson)

    implementation(libraries.predixAudit) {
        exclude(module: 'httpclient')
        //excluded to avoid bringing in jackson core < 2.9
        exclude(module: 'predix-event-hub-sdk')
        exclude(group: 'org.hibernate')
    }
    implementation(libraries.springLogFilter) {
        exclude(group: 'ch.qos.logback')
    }

    runtimeOnly(libraries.splunkObservability)

    testImplementation(project(":cloudfoundry-identity-model").sourceSets.test.output)

    testImplementation(libraries.springTest)

    testImplementation(libraries.junit)
    testImplementation(libraries.mockito)

    testImplementation(libraries.mockitoJunit5)

    testImplementation(libraries.postgresql)

    testImplementation(libraries.tomcatElApi)
    testImplementation(libraries.tomcatJasperEl)
    testImplementation(libraries.tomcatJdbc)

    testImplementation(libraries.jsonPathAssert)
    testImplementation(libraries.guavaTestLib)
}

configurations.all {
    exclude(group: "org.beanshell", module: "bsh-core")
    exclude(group: "org.apache-extras.beanshell", module: "bsh")
}

jar {
    exclude("org/cloudfoundry/identity/uaa/web/tomcat/UaaStartupFailureListener.*")
}

processResources {
    //maven replaces project.artifactId in the log4j.properties file
    //https://www.pivotaltracker.com/story/show/74344574
    filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}', 'cloudfoundry-identity-server') : line }
}

integrationTest {}.onlyIf { //disable since we don't have any
    false
}

task tomcatListenerJar(type: Jar) {
    archiveBaseName = "tomcat-listener"
    from(sourceSets.main.output)
    include("org/cloudfoundry/identity/uaa/web/tomcat/UaaStartupFailureListener.*")
}

artifacts {
    archives(tomcatListenerJar)
}
