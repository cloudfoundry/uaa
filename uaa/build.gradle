Project identityServer = parent.subprojects.find { it.name.equals("cloudfoundry-identity-server") }

apply(plugin: "war")
apply(plugin: "org.asciidoctor.convert")

processResources {
    //maven replaces project.artifactId in the log4j.properties file
    //https://www.pivotaltracker.com/story/show/74344574
    from(new File('../common/src/main/resources/log4j.properties'))
    filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}', 'cloudfoundry-identity-uaa') : line }
}

apply(plugin: "eclipse-wtp")
apply(plugin: "org.springframework.boot")
bootWar { enabled = false }
war { enabled = true }

eclipse {
    wtp {
        component {
            contextPath = "uaa"
        }
    }
}

repositories {
    maven { url("https://repo.spring.io/libs-milestone") }
}

description = "UAA"
dependencies {
    implementation(project(":cloudfoundry-identity-server")) {
        exclude(module: "jna")
    }
    implementation(project(":cloudfoundry-identity-statsd-lib"))
    implementation(libraries.springBootStarter)
    implementation(libraries.springBootStarterWeb)
    runtimeOnly(libraries.springSecurityConfig)
    runtimeOnly(libraries.springRetry)
    runtimeOnly(libraries.aspectJWeaver)
    runtimeOnly(libraries.postgresql)

    implementation(libraries.javaxXmlBindApi)
    implementation(libraries.javaxXmlBindCore)
    implementation(libraries.javaxXmlBindImpl)

    providedCompile(libraries.tomcatEmbed)

    testImplementation(identityServer.configurations.testImplementation.dependencies)
    testImplementation(identityServer.sourceSets.test.output)

    testImplementation(libraries.apacheDsProtocolLdap) {
        exclude(module: "bcprov-jdk15")
        exclude(module: "slf4j-api")
        exclude(module: "slf4j-log4j12")
    }
    testImplementation(libraries.junit)
    testImplementation(libraries.selenium)
    testImplementation(libraries.dumbster)
    testImplementation(libraries.zxing)
    testImplementation(libraries.jsonAssert)
    testImplementation(libraries.springTest)
    testImplementation(libraries.springSecurityTest)
    testImplementation(libraries.mockito)
    testImplementation(libraries.tomcatJdbc)
    testImplementation(libraries.springRestdocs)
    testImplementation(libraries.greenmail)
}

ext {
    snippetsDir = file("build/generated-snippets")
}

test {
    exclude("org/cloudfoundry/identity/uaa/integration/*.class")
    exclude("**/*IT.class")
    exclude("**/*Docs.class")
    systemProperty("mock.suite.test", "true")
}

task populateVersionfile {
    def versionFile = new File("$projectDir/slateCustomizations/source/versionfile")
    versionFile.createNewFile()
    assert versionFile.exists()
    versionFile.text = version.toString().substring(0, version.toString().lastIndexOf(".")) + ".0"
}
task customizeSlate(type: Copy) {
    dependsOn populateVersionfile
    from("slate")
    from("slateCustomizations")
    into("build/slate")
}
task docsTestRestDocs(type: Test) {
    useJUnitPlatform()
    include("**/*Docs.class")
    systemProperty("docs.build.generated.snippets.dir", snippetsDir.getPath())
}

task(bundleInstall, type: Exec) {
    dependsOn("customizeSlate")
    workingDir(file("build/slate"))
    executable("bundle")
    args("install")
}

task(deleteDefaultContent, type: Delete) {
    delete("build/slate/source/index.html.md")
}

task(slate, type: Exec) {
    dependsOn("customizeSlate", "deleteDefaultContent", "bundleInstall", "docsTestRestDocs")
    workingDir(file("build/slate"))
    executable("bundle")
    args("exec", "middleman", "build", "--verbose", "--build-dir=../docs/version/" + version.toString().substring(0, version.toString().lastIndexOf(".")) + ".0")
}

generateDocs {
    dependsOn(slate)
}

integrationTest {
    filter {
        includeTestsMatching("org.cloudfoundry.identity.uaa.integration.*")
        includeTestsMatching("*IT")
    }
}

war {
    //workaround for maven <scope>optional</scope>
    rootSpec.exclude("**/spring-security-oauth-*.jar")
}
