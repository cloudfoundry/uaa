Project identityServer = parent.subprojects.find { it.name.equals('cloudfoundry-identity-server') }

apply plugin: 'war'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.moowork.node'

processResources {
  //maven replaces project.artifactId in the log4j.properties file
  //https://www.pivotaltracker.com/story/show/74344574
  from(new File('../common/src/main/resources/log4j.properties'))
  filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}','cloudfoundry-identity-uaa') : line }
}

apply plugin: 'eclipse-wtp'
eclipse {
  wtp {
    component {
      contextPath = 'uaa'
    }
  }
}

repositories {
  maven { url 'https://repo.spring.io/libs-milestone' }
}

description = 'UAA'
dependencies {
  compile(project(':cloudfoundry-identity-server')){
    exclude(module: 'jna')
  }
  compile group: 'cglib', name: 'cglib', version:parent.cglibVersion
  runtime group: 'org.springframework.security', name: 'spring-security-config', version:parent.springSecurityVersion
  runtime group: 'org.springframework.retry', name: 'spring-retry', version:parent.springRetryVersion
  runtime group: 'org.aspectj', name: 'aspectjweaver', version:parent.aspectJVersion
  runtime group: 'org.postgresql', name: 'postgresql', version:parent.postgresqlVersion
  providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: parent.servletVersion

  testCompile identityServer.configurations.testCompile.dependencies
  testCompile identityServer.sourceSets.test.output

  testCompile(group: 'org.apache.directory.server', name: 'apacheds-core', version:'1.5.5') {
    exclude(module: 'bcprov-jdk15')
    exclude(module: 'slf4j-api')
    exclude(module: 'slf4j-log4j12')
  }
  testCompile(group: 'org.apache.directory.server', name: 'apacheds-protocol-ldap', version:'1.5.5') {
    exclude(module: 'bcprov-jdk15')
    exclude(module: 'slf4j-api')
    exclude(module: 'slf4j-log4j12')
  }
  testCompile group: 'junit', name: 'junit', version: parent.junitVersion
  testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: parent.seleniumJavaVersion
  testCompile group: 'dumbster', name: 'dumbster', version:'1.6'
  testCompile group: 'com.google.zxing', name: 'core', version: parent.zxingVersion
  testCompile group: 'org.reflections', name: 'reflections', version: '0.9.10'
  testCompile group: 'org.skyscreamer', name:'jsonassert', version: '0.9.0'
  testCompile group: 'org.springframework', name: 'spring-test', version: parent.springVersion
  testCompile group: 'org.springframework.security', name: 'spring-security-test', version: parent.springSecurityVersion
  testCompile group: 'org.mockito', name: 'mockito-core', version: parent.mockitoVersion
  testCompile group: 'org.apache.tomcat', name: 'tomcat-jdbc', version:parent.tomcatJdbcPoolVersion
  testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.2.0.RC1'
}

node {
  npmVersion = '2.1.5'

  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  download = true

  // Set the work directory for NPM
  npmWorkDir = file("${project.buildDir}/npm")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}")
}
task jasmineTests {
  dependsOn(npm_install)
  dependsOn(npm_test)
}

ext {
  snippetsDir = file('build/generated-snippets')
}
test {
  exclude 'org/cloudfoundry/identity/uaa/integration/*.class'
  exclude '**/*IT.class'
  systemProperty "mock.suite.test", "true"
}

task populateVersionfile {
  def versionfile = new File("$projectDir/slateCustomizations/source/versionfile")
  versionfile.createNewFile()
  assert versionfile.exists()
  versionfile.text = version.toString().substring(0, version.toString().lastIndexOf('.')) + ".0"
}
task customizeSlate(type: Copy) {
  dependsOn populateVersionfile
  from 'slate'
  from 'slateCustomizations'
  into 'build/slate'
}
task docsTestRestDocs(type: Test) {
  useJUnitPlatform()

  include '**/DefaultDocsTestSuite.class'
  include '**/AuthorizeEndpointDocs.class'
  include '**/CheckTokenEndpointDocs.class'
  include '**/TokenEndpointDocs.class'
  include '**/JwtBearerGrantDocs.class'
  include '**/ClientAdminEndpointsDocs.class'
  include '**/ClientMetadataAdminEndpointsDocs.class'
  include '**/IdentityProviderEndpointsDocs.class'
}

task(bundleInstall, type: Exec) {
  dependsOn 'customizeSlate'
  workingDir file('build/slate')
  executable 'bundle'
  args 'install'
}

task(deleteDefaultContent, type: Delete) {
    delete 'build/slate/source/index.html.md'
}


task(slate, type: Exec) {
  dependsOn 'customizeSlate', 'deleteDefaultContent', 'bundleInstall', 'docsTestRestDocs'
      workingDir file('build/slate')
      executable 'bundle'
      args 'exec', 'middleman', 'build', '--verbose', '--build-dir=../docs/version/' + version.toString().substring(0, version.toString().lastIndexOf('.')) + ".0"
}
generateDocs {
  dependsOn slate
}

integrationTest {
  filter {
    includeTestsMatching "org.cloudfoundry.identity.uaa.integration.*"
    includeTestsMatching "*IT"
  }
}

war {
  //workaround for maven <scope>optional</scope>
  rootSpec.exclude("**/spring-security-oauth-*.jar")
}

  

publishing {
  publications {
    uaa_war(MavenPublication) {
      apply plugin: 'war'
      groupId 'org.cloudfoundry.identity'
      artifactId 'cloudfoundry-identity-uaa'

      from components.web
      artifact sourceJar
    }

  }
}
