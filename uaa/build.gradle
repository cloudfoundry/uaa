Project identityServer = parent.subprojects.find { it.name.equals("cloudfoundry-identity-server") }

apply(plugin: "war")
apply(plugin: "org.asciidoctor.convert")

processResources {
    //maven replaces project.artifactId in the log4j.properties file
    //https://www.pivotaltracker.com/story/show/74344574
    from(new File('../common/src/main/resources/log4j.properties'))
    filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}', 'cloudfoundry-identity-uaa') : line }
}

apply(plugin: "org.springframework.boot")
bootWar { enabled = false }
jar { enabled = false }
war { archiveClassifier = '' }
war { enabled = true }


repositories {
    maven { url("https://repo.spring.io/libs-milestone") }
}

description = "UAA"
dependencies {
    implementation(project(":cloudfoundry-identity-server")) {
        exclude(module: "jna")
    }
    implementation(project(":zone-service"))
    implementation(project(":cloudfoundry-identity-statsd-lib"))
    implementation(libraries.springBootStarter)
    implementation(libraries.springBootStarterWeb){
        exclude(group: "org.glassfish", module: "jakarta.el")
    }
    implementation(libraries.springSecurityOauth) {
        exclude(module: "commons-codec")
        exclude(module: "jackson-mapper-asl")
        exclude(module: "spring-security-web")
    }
    implementation(libraries.thymeLeaf) {
        exclude(module: "ognl")
    }
    implementation(libraries.thymeleafSpring5) {
        exclude(module: "ognl")
    }
    runtimeOnly(libraries.springSecurityConfig)
    runtimeOnly(libraries.springRetry)
    runtimeOnly(libraries.aspectJWeaver)
    runtimeOnly(libraries.postgresql)

    implementation(libraries.javaxXmlBindApi)
    implementation(libraries.jakartaEl)
    implementation(libraries.glassfishJaxb)

    providedCompile(libraries.tomcatEmbed)

    implementation(libraries.springMeteringFilter) {
        exclude(group: "javax.mail")
    }
    //implementation(libraries.dynamicLogConfigurer)
    runtimeOnly(libraries.nurego)

    testImplementation(identityServer.sourceSets.test.output)

    testImplementation(project(":cloudfoundry-identity-model"))
    testImplementation(project(":cloudfoundry-identity-metrics-data"))
    testImplementation(libraries.apacheDsProtocolLdap) {
        exclude(module: "bcprov-jdk15")
        exclude(module: "slf4j-api")
        exclude(module: "slf4j-log4j12")
    }
    testImplementation(libraries.apacheLdapApi) {
        exclude(module: "slf4j-api")
    }
    testImplementation(libraries.flywayCore)
    testImplementation(libraries.googleAuth)
    testImplementation(libraries.hibernateValidator)
    testImplementation(libraries.junit)
    testImplementation(libraries.selenium)
    testImplementation(libraries.dumbster) {
        exclude(module: "mail")
        exclude(module: "activation")
    }
    testImplementation(libraries.zxing)
    testImplementation(libraries.jsonAssert)
    testImplementation(libraries.jsonPathAssert)
    testImplementation(libraries.unboundIdScimSdk) {
        exclude(module: "servlet-api")
        exclude(module: "commons-logging")
        exclude(module: "httpclient")
        exclude(module: "wink-client-apache-httpclient")
    }
    testImplementation(libraries.springBootStarterLog4j2)
    testImplementation(libraries.springContextSupport)
    testImplementation(libraries.springSessionJdbc)
    testImplementation(libraries.springTest)
    testImplementation(libraries.springSecurityJwt)
    testImplementation(libraries.springSecurityLdap)
    testImplementation(libraries.springSecuritySaml)
    testImplementation(libraries.springSecurityTest)
    testImplementation(libraries.mockito)
    testImplementation(libraries.tomcatJdbc)
    testImplementation(libraries.springRestdocs)
    testImplementation(libraries.javaxMail)
    testImplementation(libraries.greenmail)
    testImplementation(libraries.lombok)

    testImplementation(libraries.nurego)

    testAnnotationProcessor(libraries.lombok)

    testImplementation(project(":iam-k8s-utils"))
}

ext {
    snippetsDir = file("build/generated-snippets")
}

test {
    exclude 'org/cloudfoundry/identity/uaa/integration/*.class'
    exclude '**/*IT.class'
    exclude '**/*AT.class'
    exclude 'org/cloudfoundry/identity/uaa/degraded/*.class'
    exclude '**/*Docs.class'
    systemProperty 'java.io.tmpdir', '/tmp'
    systemProperty("mock.suite.test", "true")
}

task populateVersionfile {
    def versionFile = new File("$projectDir/slateCustomizations/source/versionfile")
    versionFile.createNewFile()
    assert versionFile.exists()
    versionFile.text = version.toString().substring(0, version.toString().lastIndexOf(".")) + ".0"
}
task customizeSlate(type: Copy) {
    dependsOn populateVersionfile
    from("slate")
    from("slateCustomizations")
    into("build/slate")
}
task docsTestRestDocs(type: Test) {
    useJUnitPlatform()
    include("**/*Docs.class")
    systemProperty("docs.build.generated.snippets.dir", snippetsDir.getPath())
}

task(bundleInstall, type: Exec) {
    dependsOn("customizeSlate")
    workingDir(file("build/slate"))
    executable("bundle")
    args("install")
}

task(deleteDefaultContent, type: Delete) {
    delete("build/slate/source/index.html.md")
}

task(slate, type: Exec) {
    dependsOn("customizeSlate", "deleteDefaultContent", "bundleInstall", "docsTestRestDocs")
    workingDir(file("build/slate"))
    executable("bundle")
    args("exec", "middleman", "build", "--verbose", "--build-dir=../docs/version/" + version.toString().substring(0, version.toString().lastIndexOf(".")) + ".0")
}

generateDocs {
    dependsOn(slate)
}

integrationTest {
    filter {
        exclude '**/*AT.class'
        exclude 'org/cloudfoundry/identity/uaa/degraded/*.class'
        includeTestsMatching("org.cloudfoundry.identity.uaa.integration.*")
        includeTestsMatching("*IT")
    }
}

task acceptanceTest(type: Test) {
  // Don't let tests be 'UP-TO-DATE' and not run
  outputs.upToDateWhen { false }
  include "org/cloudfoundry/identity/uaa/acceptance/*.class"
    // Exclude Selenium chromedriver based SAML login test if UAA is being run locally
    if (System.getenv('DEPLOYMENT_TYPE') == 'local') {
        exclude "org/cloudfoundry/identity/uaa/acceptance/SamlLoginAT.class"
    }
}

task degradedTestCloud(type: Test) {
  systemProperty "RUN_AGAINST_CLOUD", true
  include "org/cloudfoundry/identity/uaa/degraded/*.class"
}

task degradedTestLocal(type: Test) {
  systemProperty "RUN_AGAINST_CLOUD", false
  dependsOn rootProject.cargoStartLocal
  finalizedBy rootProject.cargoStopLocal
  include "org/cloudfoundry/identity/uaa/degraded/*.class"
}

task orchestratorIntegrationTest(type: Test) {
    // Don't let tests be 'UP-TO-DATE' and not run
    outputs.upToDateWhen { false }
    include "org/cloudfoundry/identity/uaa/orchestrator/*.class"
}

war {
    //workaround for maven <scope>optional</scope>
    rootSpec.exclude("**/spring-security-oauth-*.jar")
}
