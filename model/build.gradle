description = 'CloudFoundry Identity Model JAR'

dependencies {

  compile project(':cloudfoundry-identity-metrics-data')

  compile group: 'javax.validation', name: 'validation-api', version: parent.validationAPIVersion

  compile group: 'commons-io', name: 'commons-io', version: '2.6'

  compile group: 'org.springframework', name: 'spring-web', version: parent.springVersion
  compile group: 'org.springframework', name: 'spring-webmvc', version: parent.springVersion
  compile group: 'org.springframework.security', name: 'spring-security-config', version: parent.springSecurityVersion

  compile (group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version: parent.springSecurityOAuthVersion) {
    exclude(module: 'jackson-mapper-asl')
    exclude(module: 'spring-security-web')
  }

  compile group: 'org.slf4j', name: 'slf4j-api', version:parent.slf4jVersion

  testCompile (group: 'junit', name: 'junit', version: parent.junitVersion) {
    exclude(module: 'hamcrest-core')
  }

}

apply from: file('build_properties.gradle')

configurations.all {
   exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

processResources {
  //maven replaces project.artifactId in the log4j.properties file
  //https://www.pivotaltracker.com/story/show/74344574
  filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}','cloudfoundry-identity-model') : line }
}

integrationTest {}.onlyIf { //disable since we don't have any
  false
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      
      pom.project {
        name 'UAA model'
        packaging 'jar'
        artifactId 'cloudfoundry-identity-model'
        description 'Cloud Foundry User Account and Authentication (UAA) Server'
        url 'https://github.com/cloudfoundry/uaa'

        scm {
          connection 'scm:git:git://github.com:cloudfoundry/uaa.git'
          developerConnection 'scm:git:ssh://github.com:cloudfoundry/uaa.git'
          url 'https://github.com/cloudfoundry/uaa'
        }

        developers {
          developer {
            id 'pcf-uaa'
            name 'PCF UAA Team'
            email 'pcf-uaa@pivotal.io'
          }
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
      }
    }
  }
}